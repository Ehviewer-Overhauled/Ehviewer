plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-kapt'
}

android {
    compileSdkVersion 33
    buildToolsVersion "33.0.0"
    ndkVersion "25.0.8775105"

    Properties localProperties = new Properties()
    if (project.rootProject.file('local.properties').exists()) {
        localProperties.load(project.rootProject.file('local.properties').newDataInputStream())
    }

    splits {
        abi {
            enable = true
        }
    }

    signingConfigs {
        sign {
            storeFile file(signStoreFile)
            storePassword signStorePassword
            keyAlias signkeyAlias
            keyPassword signkeyPassword
        }
    }

    defaultConfig {
        applicationId "moe.tarsin.ehviewer"
        minSdkVersion 31
        targetSdkVersion 33
        versionCode 180004
        versionName "1.8.0.4"
        resConfigs 'zh', 'zh-rCN', 'zh-rHK', 'zh-rTW', 'es', 'ja', 'ko', 'fr', 'de', 'th'

        externalNativeBuild {
            cmake {
                arguments '-DANDROID_STL=none'
            }
        }
    }

    externalNativeBuild {
        cmake {
            path 'src/main/cpp/CMakeLists.txt'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    kotlinOptions {
        jvmTarget = '11'
    }

    lint {
        abortOnError true
        checkReleaseBuilds false
        disable 'MissingTranslation'
    }

    packagingOptions {
        resources {
            excludes += "/META-INF/**"
            excludes += "/kotlin/**"
            excludes += "**.txt"
            excludes += "**.bin"
        }
    }

    dependenciesInfo.includeInApk false

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles 'proguard-rules.pro'
            signingConfig signingConfigs.sign
        }
        debug {
            applicationIdSuffix ".debug"
            signingConfig signingConfigs.debug
        }
    }

    sourceSets {
        main {
            java.srcDirs += 'src/main/java-gen'
        }
    }

    namespace 'com.hippo.ehviewer'
}

tasks.withType(JavaCompile) {
    task -> task.dependsOn ":daogenerator:executeDaoGenerator"
}

dependencies {
    def activity_version = "1.6.0-alpha05"
    implementation "androidx.activity:activity:$activity_version"
    implementation "androidx.activity:activity-ktx:$activity_version"

    implementation "androidx.annotation:annotation:1.4.0"
    implementation "androidx.annotation:annotation-experimental:1.2.0"
    implementation "androidx.appcompat:appcompat:1.6.0-alpha05"

    def arch_core_version = "2.1.0"
    implementation "androidx.arch.core:core-common:$arch_core_version"
    implementation "androidx.arch.core:core-runtime:$arch_core_version"

    implementation "androidx.biometric:biometric-ktx:1.2.0-alpha04"
    implementation "androidx.browser:browser:1.4.0"

    def collection_version = "1.3.0-alpha02"
    implementation "androidx.collection:collection:$collection_version"
    implementation "androidx.collection:collection-ktx:$collection_version"

    implementation "androidx.constraintlayout:constraintlayout:2.2.0-alpha03"
    implementation "androidx.coordinatorlayout:coordinatorlayout:1.2.0"

    def core_version = "1.9.0-alpha05"
    implementation "androidx.core:core:$core_version"
    implementation "androidx.core:core-ktx:$core_version"

    implementation "androidx.customview:customview:1.2.0-alpha01"
    implementation "androidx.documentfile:documentfile:1.1.0-alpha01"
    implementation "androidx.dynamicanimation:dynamicanimation:1.1.0-alpha03"

    def fragment_version = "1.5.1"
    implementation "androidx.fragment:fragment:$fragment_version"
    implementation "androidx.fragment:fragment-ktx:$fragment_version"

    def lifecycle_version = "2.6.0-alpha01"
    implementation "androidx.lifecycle:lifecycle-livedata:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-livedata-core-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-process:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-savedstate:$lifecycle_version"

    implementation "androidx.loader:loader:1.1.0"

    def nav_version = "2.5.1"
    implementation "androidx.navigation:navigation-fragment:$nav_version"
    implementation "androidx.navigation:navigation-ui:$nav_version"
    implementation "androidx.navigation:navigation-fragment-ktx:$nav_version"
    implementation "androidx.navigation:navigation-ui-ktx:$nav_version"

    implementation 'androidx.preference:preference:1.2.0'
    implementation 'androidx.recyclerview:recyclerview:1.3.0-beta01'
    implementation 'androidx.startup:startup-runtime:1.2.0-alpha01'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.2.0-alpha01'
    implementation "androidx.transition:transition-ktx:1.4.1"
    implementation "androidx.vectordrawable:vectordrawable:1.2.0-beta01"
    implementation "androidx.vectordrawable:vectordrawable-animated:1.1.0"
    implementation 'androidx.viewpager:viewpager:1.1.0-alpha01'
    implementation 'androidx.viewpager2:viewpager2:1.1.0-beta01'
    implementation 'androidx.webkit:webkit:1.5.0-beta01'

    implementation 'com.drakeet.drawer:drawer:1.0.3'
    implementation 'com.google.android.material:material:1.7.0-alpha03'
    implementation 'com.h6ah4i.android.widget.advrecyclerview:advrecyclerview:1.0.0'
    implementation 'com.squareup.okhttp3:okhttp:5.0.0-alpha.10'
    implementation 'dev.rikka.rikkax.core:core-ktx:1.4.1'
    implementation 'dev.rikka.rikkax.insets:insets:1.3.0'
    implementation 'dev.rikka.rikkax.layoutinflater:layoutinflater:1.2.0'
    implementation 'dev.rikka.rikkax.preference:simplemenu-preference:1.0.3'
    implementation 'dev.rikka.rikkax.material:material-preference:2.0.0'
    implementation 'org.greenrobot:greendao:3.3.0'
    implementation 'org.jsoup:jsoup:1.15.2'
}

configurations.all {
    exclude group: 'dev.rikka.rikkax.appcompat', module: 'appcompat'
    exclude group: 'dev.rikka.rikkax.material', module: 'material'
}
