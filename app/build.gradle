apply plugin: 'com.android.application'
apply plugin: 'com.google.android.gms.oss-licenses-plugin'

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.2"
    ndkVersion "21.3.6528147"
    signingConfigs {
        def keystorePwd = null
        def alias = null
        def pwd = null
        if (project.rootProject.file('local.properties').exists()) {
            Properties properties = new Properties()
            properties.load(project.rootProject.file('local.properties').newDataInputStream())
            keystorePwd = properties.getProperty("RELEASE_STORE_PASSWORD")
            alias = properties.getProperty("RELEASE_KEY_ALIAS")
            pwd = properties.getProperty("RELEASE_KEY_PASSWORD")
        }
        release {
            storeFile file("hitokoto.jks")
            storePassword keystorePwd != null ? keystorePwd : System.getenv("KEYSTORE_PASS")
            keyAlias alias != null ? alias : System.getenv("ALIAS_NAME")
            keyPassword pwd != null ? pwd : System.getenv("ALIAS_PASS")
        }
    }
    defaultConfig {
        applicationId "com.hippo.ehviewer"
        minSdkVersion 21
        targetSdkVersion 30
        versionCode 113
        versionName "1.7.10"
        resConfigs "zh", "zh-rCN", "zh-rHK", "zh-rTW",
                "es", "ja", "ko", "fr", "de", "th"
        testOptions.unitTests.includeAndroidResources = true
        ndk {
            abiFilters "armeabi-v7a", "arm64-v8a", "x86", "x86_64"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lintOptions {
        disable 'MissingTranslation'
        abortOnError true
        checkReleaseBuilds true
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            buildConfigField 'String', 'FILE_PROVIDER_AUTHORITY', '"com.hippo.ehviewer.fileprovider"'
            signingConfig signingConfigs.release
        }
        debug {
            applicationIdSuffix ".debug"
            buildConfigField 'String', 'FILE_PROVIDER_AUTHORITY', '"com.hippo.ehviewer.debug.fileprovider"'
            signingConfig signingConfigs.release
        }
    }

    sourceSets {
        main {
            java.srcDirs += 'src/main/java-gen'
        }
    }
}

tasks.withType(JavaCompile) {
    task -> task.dependsOn ":daogenerator:executeDaoGenerator"
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.biometric:biometric:1.0.1'
    implementation "androidx.browser:browser:1.2.0"
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'com.google.android.gms:play-services-oss-licenses:17.0.0'
    implementation 'com.google.android.material:material:1.2.1'
    implementation 'com.getkeepsafe.relinker:relinker:1.4.1'
    implementation 'com.github.amlcurran.showcaseview:library:5.4.3'
    implementation 'com.github.seven332.a7zip:extract-lite:1b21783'
    implementation 'com.github.seven332:android-resource:0.1.0'
    implementation 'com.github.seven332:beerbelly:0.1.4'
    implementation 'com.github.seven332:conaco:0.1.5-eh'
    implementation 'com.github.seven332:easyrecyclerview:0.1.1'
    implementation 'com.github.seven332:glgallery:0.1.2'
    implementation 'com.github.seven332:glview:0.1.0'
    implementation 'com.github.seven332:glview-image:0.1.0'
    implementation 'com.github.seven332:hotspot:0.1.0'
    implementation 'com.github.seven332:image:0.1.12'
    implementation 'com.github.seven332:refreshlayout:0.1.0'
    implementation 'com.github.seven332:streampipe:0.1.0'
    implementation 'com.github.seven332:tuxiang:0.1.6'
    implementation 'com.github.seven332:unifile:9ec57bcd8f'
    implementation 'com.github.seven332:yorozuya:0.1.2'
    implementation 'com.github.seven332:yorozuya-thread:0.1.1'
    implementation 'com.github.seven332:yorozuya-collect:0.1.4'
    implementation 'com.h6ah4i.android.widget.advrecyclerview:advrecyclerview:1.0.0'
    def appCenterSdkVersion = '3.3.1'
    implementation "com.microsoft.appcenter:appcenter-analytics:${appCenterSdkVersion}"
    implementation "com.microsoft.appcenter:appcenter-crashes:${appCenterSdkVersion}"
    implementation "com.microsoft.appcenter:appcenter-distribute:${appCenterSdkVersion}"
    implementation 'com.squareup.okhttp3:okhttp:3.14.7'
    implementation 'com.takisoft.preferencex:preferencex:1.1.0'
    implementation 'tech.rectifier.preferencex-android:preferencex-simplemenu:88f93154b2'
    implementation 'org.ccil.cowan.tagsoup:tagsoup:1.2.1'
    implementation 'org.greenrobot:greendao:2.2.1'
    implementation 'org.jsoup:jsoup:1.12.2'
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.robolectric:robolectric:4.2.1'
    testImplementation 'org.jooq:joor:0.9.6'
}

configurations.all {
    resolutionStrategy {
        force 'com.github.seven332:glgallery:25893283ca'
        force 'com.github.seven332:glview:ba6aee61d7'
        force 'com.github.seven332:glview-image:68d94b0fc2'
        force 'com.github.seven332:image:09b43c0c68'

        exclude group: 'com.github.seven332', module: 'okhttp'
    }
}
